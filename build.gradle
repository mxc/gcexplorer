subprojects {
    apply plugin: 'java'
    sourceCompatibility = '1.8'
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
    

    
    ext {
        vendor = 'Jumping Bean'
        appversion = '0.1'
        author = 'Mark Clarke'
        title = 'GCExplorer'
        user = System.getenv()['USER']
    }
    
    repositories {
        mavenCentral()
    }

    dependencies{
        testCompile group: 'junit', name: 'junit', version: '4.11'
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'       
        compile group: 'javax.inject', name: 'javax.inject', version: '1'
    }

    jar {
        manifest {
            attributes  'Created-By': author + ' '+  vendor
            attributes  'Built-By': user
            attributes  'Build-Date': new Date()
            attributes  'Implementation-Version': "${appversion}"
        }
    }

}

project (':GCExplorerUI'){
    
    project.ext.mainClass = "za.co.jumpingbean.gcexplorer.ui.Main"
    apply from: 'http://dl.bintray.com/content/shemnon/javafx-gradle/0.4.0/javafx.plugin'    
    
    ext{ category = 'System'
        copyright = 'Copyright (c) 2014 Jumping Bean'
        description = 'A tool for learning about Java memory management.'
        licenseType = 'GPL v3'
        vendor = 'Jumping Bean'
        installSystemWide = true
        menu = true
        shortcut = true
    }
    dependencies {
        compile project (':GCExplorerService')
    }

    sourceSets {
        main { 
            output.resourcesDir = "build/classes/main" 
        }
    }
    
    jar {
        manifest {
            attributes  'Main-Class': 'za.co.jumpingbean.gcexplorer.ui.Main'
            attributes  'Implementation-Title': title
        }   
    }

}



project (':GCExplorerService'){
    
    dependencies {
        compile project (':GarbageGeneratorApp')
        testCompile group: 'org.javassist', name: 'javassist', version: '3.18.2-GA'
    }
    
    jar {
        manifest {
            attributes  'Implementation-Title': 'GCExplorerService'
        }
    }
    
}

project (':GarbageGeneratorApp'){

    jar {
        from {
            configurations.compile.collect { 
                it.isDirectory() ? it : zipTree(it) 
            }
        }
        manifest {
            attributes "Main-Class":  "za.co.jumpingbean.gc.Generator"
            attributes  'Implementation-Title': 'GarbageGeneratorApp'
        }
    }

}
